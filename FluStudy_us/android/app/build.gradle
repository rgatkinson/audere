buildscript {
  repositories {
    google()
    maven { url 'https://maven.fabric.io/public' }
  }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Utils for managing version and build number
import groovy.json.JsonSlurper

def getNpmVersion() {
  def inputFile = new File('../package.json')
  def packageJson = new JsonSlurper().parseText(inputFile.text)
  return packageJson['version']
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()
    if (versionPropsFile.canRead()) {
      versionProps.load(new FileInputStream(versionPropsFile))
    }
    def code = versionProps['VERSION_CODE'].toInteger()

    applicationId 'org.auderenow.fluathome_us'
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode code
    versionName getNpmVersion()
    multiDexEnabled true
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    // Deprecated. Used by net.openid:appauth
    manifestPlaceholders = [
      'appAuthRedirectScheme': 'host.exp.exponent'
    ]
    minSdkVersion 22
  }

  dexOptions {
    javaMaxHeapSize System.getenv("DISABLE_DEX_MAX_HEAP") ? null : "8g"
  }

  buildTypes {
    debug {
      debuggable true
      manifestPlaceholders = [usesCleartextTraffic:"true"]
    }
    release {
      manifestPlaceholders = [usesCleartextTraffic:"false"]
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      zipAlignEnabled true
    }
  }
  aaptOptions {
    cruncherEnabled false
    useNewCruncher false
    noCompress "tflite"
  }

  signingConfigs {
    debug {
      storeFile file('../debug.keystore')
    }
  }
  lintOptions {
    abortOnError false
  }
  packagingOptions {
    pickFirst "**"
  }
  configurations.all {
    resolutionStrategy.force 'com.android.support:design:28.0.0'
  }
  splits {
    // https://developer.android.com/studio/build/configure-apk-splits
    abi {
      enable true
      reset()
      include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
      universalApk false
    }
  }
  compileOptions {
    sourceCompatibility 1.8
    targetCompatibility 1.8
  }
}

// https://developer.android.com/studio/build/configure-apk-splits.html
// If versionCode (build number) from version.properties is 88,
// then ARM versionCode will be 10088 and x86 versionCode will be 20088.
ext.abiCodes = ['armeabi-v7a':1, 'x86':2, 'arm64-v8a':3, 'x86_64':4]
import com.android.build.OutputFile
android.applicationVariants.all { variant ->
  variant.outputs.each { output ->
    def baseAbiVersionCode =
            project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
    if (baseAbiVersionCode != null) {
      output.versionCodeOverride =
              baseAbiVersionCode * 10000 + variant.versionCode
    }
  }
}

configurations.all {
  resolutionStrategy {
    force 'org.webkit:android-jsc:+'
  }
  resolutionStrategy {
    force "com.google.android.gms:play-services-tasks:16.0.1"
  }
  resolutionStrategy {
    force "com.google.android.gms:play-services-gcm:16.1.0"
  }
}


apply from: 'expo.gradle'

apply from: "../../node_modules/react-native-unimodules/gradle.groovy"

dependencies {
  implementation project(':react-native-referrer') 
  implementation project(':react-native-modal-translucent')
  implementation 'org.tensorflow:tensorflow-lite:0.0.0-nightly'
  implementation project(':react-native-push-notification')
  implementation project(':react-native-fbsdk')
  implementation project(':react-native-firebase')
  implementation project(':react-native-get-random-values')
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation 'com.google.firebase:firebase-core:16.0.9'
  implementation 'com.google.firebase:firebase-perf:17.0.2'
  implementation 'com.google.firebase:firebase-config:17.0.0'
  implementation 'com.google.firebase:firebase-messaging:18.0.0'
  implementation 'com.google.firebase:firebase-firestore:19.0.1'
  implementation 'com.google.firebase:firebase-storage:17.0.0'
  implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
  implementation project(':react-native-device-info')
  implementation 'com.android.support:multidex:1.0.1'
  // Our dependencies
  // Our dependencies from ExpoView
  // DON'T ADD ANYTHING HERE THAT ISN'T IN EXPOVIEW. ONLY COPY THINGS FROM EXPOVIEW TO HERE.
  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'com.facebook.android:facebook-android-sdk:4.37.0'
  implementation('com.facebook.android:audience-network-sdk:4.99.0') {
    exclude module: 'play-services-ads'
  }
  compileOnly 'org.glassfish:javax.annotation:3.1.1'
  implementation 'com.jakewharton:butterknife:8.4.0'
  implementation 'de.greenrobot:eventbus:2.4.0'
  implementation 'com.amplitude:android-sdk:2.9.2'
  // Be careful when upgrading! Upgrading might break experience scoping. Check with Jesse. See Analytics.resetAmplitudeDatabaseHelper
  implementation 'com.squareup.picasso:picasso:2.5.2'
  implementation 'com.google.android.gms:play-services-base:16.1.0'
  implementation 'com.google.android.gms:play-services-flags:16.0.1'
  implementation 'com.google.android.gms:play-services-gcm:16.1.0'
  implementation 'com.google.android.gms:play-services-analytics:16.0.8'
  implementation 'com.google.android.gms:play-services-maps:16.1.0'
  implementation 'com.google.android.gms:play-services-auth:16.0.1'
  implementation 'com.google.android.gms:play-services-location:16.0.0'
  implementation 'com.google.android.gms:play-services-ads:17.2.1'
  implementation 'com.madgag.spongycastle:core:1.53.0.0'
  implementation 'com.madgag.spongycastle:prov:1.53.0.0'
  debugImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
  // debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.4-beta1'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
  implementation 'com.facebook.device.yearclass:yearclass:1.0.1'
  implementation 'commons-io:commons-io:1.3.2'
  implementation 'me.leolin:ShortcutBadger:1.1.8@aar'
  implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
  implementation 'com.theartofdev.edmodo:android-image-cropper:2.4.7'
  implementation 'com.yqritc:android-scalablevideoview:1.0.4'
  implementation 'commons-codec:commons-codec:1.10'
  implementation 'com.segment.analytics.android:analytics:4.3.0'
  implementation 'com.google.zxing:core:3.2.1'
  implementation 'net.openid:appauth:0.4.1'
  implementation('com.airbnb.android:lottie:2.5.5') {
    exclude group: 'com.android.support', module: 'appcompat-v7'
  }
  implementation('io.nlopez.smartlocation:library:3.2.11') {
    transitive = false
  }
  implementation 'com.android.support:exifinterface:28.0.0'
  implementation 'com.squareup.okio:okio:1.9.0'
  implementation 'com.facebook.soloader:soloader:0.5.1'
  implementation 'com.google.android.exoplayer:exoplayer:2.6.1'
  // expo-file-system
  implementation 'com.google.android.exoplayer:extension-okhttp:2.6.1'
  // RDT Reader
  implementation 'androidx.constraintlayout:constraintlayout:1.1.2'
  // Testing
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
  // We use a modified build of com.android.support.test:runner:1.0.1. Explanation in maven-test/README
  androidTestImplementation 'com.android.support.test:runner:1.0.1'
  androidTestImplementation 'com.android.support:support-annotations:28.0.0'
  androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.0'
  androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
  androidTestImplementation 'com.azimolabs.conditionwatcher:conditionwatcher:0.2'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:1.10.19'
  testImplementation 'org.robolectric:robolectric:3.8'
  testImplementation 'com.android.support.test:runner:1.0.2-alpha1'
  testImplementation 'com.android.support.test:rules:1.0.2-alpha1'
  implementation('host.exp.exponent:expoview:35.0.0@aar') {
    transitive = true
    exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    exclude group: 'com.squareup.okhttp3', module: 'okhttp-urlconnection'
  }
  implementation project(':react-native-video')
  addUnimodulesDependencies([
    modulesPaths : [
      '../../node_modules'
    ],
    configuration: 'api',
    target       : 'react-native',
    exclude      : [
      'expo-ads-admob',
      'expo-av',
      'expo-calendar',
      'expo-contacts',
      'expo-crypto',
      'expo-document-picker',
      'expo-face-detector',
      'expo-face-detector-interface',
      'expo-google-signin',
      'expo-local-authentication',
      'expo-mail-composer',
      'expo-media-library',
      'expo-print',
      'expo-sms',
      'expo-speech',
      'expo-sqlite',
    ]
  ])
  implementation project(path: ':openCVLibrary341-contrib')
  implementation project(':react-native-aes-crypto')
}

// This has to be down here for some reason
apply plugin: 'com.google.gms.google-services'
